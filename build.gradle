
// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "7.0.0-MC1.8.9"
group = "dev.nero.bettercolors.main"
archivesBaseName = "bettercolors"

// Important ! The code uses lambda expressions & diamond operators
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.8.9-11.15.1.1722"
    runDir = "run"
    mappings = "stable_22" // http://export.mcpbot.bspk.rs/stable/?page=2
}

configurations {
    inJar
    compile.extendsFrom inJar
}

dependencies {
    inJar 'io.github.vincenzopalazzo:material-ui-swing:1.1.1_beta'
}

jar {
    into('META-INF/libraries') {
        from configurations.inJar
    }

    manifest {
        attributes(
                'ContainedDeps': 'material-ui-swing-1.1.1_beta.jar'
        )
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
